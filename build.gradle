apply plugin: 'groovy'
apply plugin: 'scala'
apply plugin: 'idea'
apply plugin: 'application'
apply plugin: 'jetty'

task testSetup(type: TestSetup)

task runLoadTest(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath

    // Gatling application
    main = "io.gatling.app.Gatling"
    // Specify the simulation to run
    args = Eval.me("['-s', 'loadTests.TestSimulation']")
}

task testTeardown(type: TestTearDown) {
}

// Orchestrate the tasks
testSetup.dependsOn jettyRun
runLoadTest.dependsOn testSetup
testTeardown.mustRunAfter runLoadTest
jettyStop.mustRunAfter testTeardown

task wrapper(type: Wrapper) {
    gradleVersion = '1.11'
}

sourceSets {
    main {
        scala {
            srcDirs = ['loadTestSrc']
        }
        groovy {
            srcDirs = ['testWebAppSrc/groovy']
        }
    }
    test {
        resources {
            srcDir "test"
        }
    }
}

sourceCompatibility = 1.8
version = '1.0'

repositories {
    mavenCentral()
    // Gatling libraries
    maven { url 'http://repository.excilys.com/content/groups/public' }
}

jettyRun {
    scanIntervalSeconds = 1
    httpPort = 8888
    webAppSourceDirectory = file('testWebAppSrc')
    daemon = true
}



dependencies {
    // Gatling dependencies
//    compile 'com.excilys.ebi.gatling:gatling-app:1.5.3'
//    compile 'com.excilys.ebi.gatling.highcharts:gatling-charts-highcharts:1.5.3'
 
    compile 'io.gatling:gatling-app:2.1.6'
    compile 'io.gatling.highcharts:gatling-charts-highcharts:2.1.6'

    compile 'org.scala-lang:scala-compiler:2.11.7'
    compile 'org.scala-lang:scala-library:2.11.7'

    // Dependencies for the test Server
    compile 'org.codehaus.groovy:groovy-all:2.2.1'
    providedCompile "javax.servlet:servlet-api:2.5"
//    testCompile 'org.eclipse.jetty.aggregate:jetty-all-server:8.1.0.v20120127'
}